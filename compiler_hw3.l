/* Definition section */
%{
#include "common.h" 
#include "y.tab.h"	/* header file generated by bison */

extern YYSTYPE yylval; //YYSTYPE
%}

/* Define regular expression label */
newline		[\n]
digit		[0-9]
letter		[a-zA-Z]
integer     {digit}+
double      {digit}+"."{digit}+
id			{letter}+({letter}|{digit})*

%option yylineno

%x COMMENT STRING_STATE

/* Rules section */
%%

 /* Arithmetic */
"+"     { return '+'; }
"-" 	{ return '-'; }
"*" 	{ return '*'; }
"/" 	{ return '/'; }
"%"     { return '%'; }
"++"    { return INC; }
"--"    { return DEC; }

 /* Relational */
">"		{ return '>'; }
"<"		{ return '<'; }
">="	{ return MTE; }
"<="	{ return LTE; }
"=="	{ return EQ; }
"!="	{ return NE; }

 /* Assignment */
"="		{ return '='; }
"+="	{ return ADDASGN; }
"-="	{ return SUBASGN; }
"*="	{ return MULASGN; }
"/="	{ return DIVASGN; }
"%="	{ return MODASGN; }

 /* Logical */
"&&"	{ return AND; }
"||"	{ return OR; }
"!"		{ return NOT; }

 /* Delimiters */
"("		{ return '('; }
")"		{ return ')'; }
"{"		{ return '{'; }
"}"		{ return '}'; }
"["		{ return '['; }
"]"		{ return ']'; }
","		{ return ','; }

 /* Print Keywords */	
"print"		{ return PRINT; }

 /* Condition and Loop Keywords */
"if"		{ return IF; }
"else"		{ return ELSE; }
"for"		{ return FOR; }
"while"		{ return WHILE; }

 /* Declaration Keywords */
"void"		{ return VOID; }
"int"		{ return INT; }
"float"  	{ return FLOAT; }
"string"  	{ return STRING; }
"bool"  	{ return BOOL; }

 /* boolean Keywords */
"true"      { return TRUE; }
"false"     { return FALSE; }

"return"    { return RETURN; }

 /* String Constant */
\"				        { BEGIN STRING_STATE; return QUOTA; }
<STRING_STATE>\"		{ BEGIN INITIAL; return QUOTA; }
<STRING_STATE>[^"]*		{ return STR_CONST; }

 /* Number Constant */
{integer}	{ return I_CONST; }
{double}	{ return F_CONST; }

 /* C type Comment */
"/*"				{ BEGIN COMMENT; }
<COMMENT>[^*\n]+	{ }
<COMMENT>\n			{ }
<COMMENT>"*"		{ }
<COMMENT>"*/"		{ BEGIN INITIAL; }

 /* C++ type Comment */
\/\/.*	{ ; }

 /* Variable ID */
{id}       { return ID; }

 /* others */

[;]         { return SEMICOLON; }
[\n]        { }
[ \t]		{ } /* Ignore */
[^ \t\n] 	{ } /* Ignore other charactor sets */
<<EOF>>     { static int once = 0; return once++ ? 0 : SEMICOLON; }

%%

int yywrap()
{
    return 1;
}
